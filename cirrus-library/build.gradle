apply plugin: 'com.android.library'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = gitLastTagWithCommitCount()

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode gitCommitCount()
        versionName gitLastTagWithCommitCount()
    }

    lintOptions {
        abortOnError false
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "source"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourceJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'Cirrus'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/inktomi/cirrus.git'
    }
}

def gitLastTagWithCommitCount() {
    def describe = grgit.describe();

    if(describe){
        def describeParts = grgit.describe().split("-")
        if(describeParts.size() == 1){
            return describeParts[0]
        } else {
            return describeParts[0] + "." + describeParts[1]
        }
    }

    return "no-tag"
}

def gitCommitCount() {
    grgit.log().size()
}

dependencies {
    compile "com.android.volley:volley:1.0.0"
    compile('org.simpleframework:simple-xml:2.7.1'){
        exclude group: 'stax'
        exclude group: 'xpp3'
    }
}
